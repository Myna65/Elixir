buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.4.0"
    id 'info.solidsoft.pitest' version '1.5.1'
}

group "be.boutiquemadame"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/vantuz/pitest-kotlin" }
}

configurations {
    ktlint
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9"
    implementation "io.arrow-kt:arrow-core:0.10.5"
    testImplementation "io.kotest:kotest-runner-junit5-jvm:4.2.0"
    testImplementation "io.kotest:kotest-assertions-core-jvm:4.2.0"
    testImplementation "io.kotest:kotest-assertions-arrow-jvm:4.2.0"
    testImplementation "io.kotest:kotest-property-jvm:4.2.0"
    ktlint "com.pinterest:ktlint:0.38.1"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}


task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

pitest {
    junit5PluginVersion = '0.12'
    threads = 16
    outputFormats = ['HTML']
}

